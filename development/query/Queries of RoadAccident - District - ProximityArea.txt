#Q1 =  All the projects in the district with the most accidents in 2022

PREFIX btp: <http://www.dei.unipd.it/~gdb/ontology/btp/>

SELECT 
    ?district 
    ?totalAccidentCount
    (GROUP_CONCAT(DISTINCT CONCAT(STR(?project), " \n = ", STR(?description)); separator = "* \n ") AS ?projectsAndDescriptions)
WHERE {
  {
    SELECT ?district (SUM(?accidents) AS ?totalAccidentCount)
    WHERE {
      ?accident a btp:RoadAccident ;
                btp:recordedInDistrict ?district ;
                btp:hasAccidentYear btp:year_2022 ;
                btp:numberOfAccidents ?accidents .
    }
    GROUP BY ?district
    ORDER BY DESC(?totalAccidentCount)
    LIMIT 1
  }

  ?district btp:containsArea ?area .
  ?area btp:isProximityArea ?project .
  ?project btp:hasDescription ?description .
  
 
}
GROUP BY ?district ?totalAccidentCount


**********************************************************************************


#Q2 - list all the pedestrian deaths compared to the average speed limit for every district

PREFIX btp: <http://www.dei.unipd.it/~gdb/ontology/btp/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT ?district ?totalPedDeaths (xsd:integer(AVG(?limit)) AS ?averageSpeedLimit)
WHERE {
    
    {
        SELECT ?district (SUM(?pedDeath) AS ?totalPedDeaths)
        WHERE {
            ?accident a btp:RoadAccident;
                      btp:recordedInDistrict ?district;
                      btp:numberOfPedestrainDeath ?pedDeath.
        }
        GROUP BY ?district
    }

    
    ?district btp:isDistrict ?road.
    ?road btp:hasArchToRoad|btp:hasArchFromRoad ?arch.
    ?arch btp:hasSpeedLimit ?splim.
    ?splim btp:speedLimit ?limit.
}
GROUP BY ?district ?totalPedDeaths
ORDER BY DESC(?totalPedDeaths)



**********************************************************************************

#Q3 - most popular type of accidents out of total accidents that happend in each district with their respected number of casualties + total traffic of districts

PREFIX btp: <http://www.dei.unipd.it/~gdb/ontology/btp/>

SELECT ?district 
       (MAX(?trafficCount) AS ?maxTraffic) 
       (SUM(?numberOfAccidents) AS ?totalAccidents)
       (MAX(?maxCasualties) AS ?mostCasualtiesInAnyCategory)
       (SAMPLE(?category) AS ?mostCasualtiesCategory)
WHERE {
  # Subquery for calculating traffic count per district
  {
    SELECT ?district (SUM(?traffic) AS ?trafficCount)
    WHERE {
      ?district a btp:District ;
                btp:isDistrict ?road .
      ?road btp:hasArchFromRoad|btp:hasArchToRoad ?roadarch .
      ?roadarch btp:isPlaced ?coil .
      ?coil btp:hasObserve ?VD .
      ?VD btp:hasCount ?traffic .
    }
    GROUP BY ?district
  }

  # Subquery for Cyclist
  {
    SELECT ?district (SUM(?cyclistInjuries + ?cyclistDeaths) AS ?totalCyclist)
    WHERE {
      ?cyclist a btp:Cyclist ;
               btp:numberOfCyclistsInjuries ?cyclistInjuries ;
               btp:numberOfCyclistsDeath ?cyclistDeaths ;
               btp:recordedInDistrict ?district .
    }
    GROUP BY ?district
  }

  # Subquery for Driver
  {
    SELECT ?district (SUM(?driverInjuries + ?driverDeaths) AS ?totalDriver)
    WHERE {
      ?driver a btp:Driver ;
              btp:numberOfDriverInjuries ?driverInjuries ;
              btp:numberOfDriverDeath ?driverDeaths ;
              btp:recordedInDistrict ?district .
    }
    GROUP BY ?district
  }

  # Subquery for Motorcyclist
  {
    SELECT ?district (SUM(?motorcyclistInjuries + ?motorcyclistDeaths) AS ?totalMotorcyclist)
    WHERE {
      ?motorcyclist a btp:Motorcyclist ;
                    btp:numberOfMotorcyclistsInjuries ?motorcyclistInjuries ;
                    btp:numberOfMotorcyclistsDeath ?motorcyclistDeaths ;
                    btp:recordedInDistrict ?district .
    }
    GROUP BY ?district
  }

  # Subquery for Pedestrian
  {
    SELECT ?district (SUM(?pedestrianInjuries + ?pedestrianDeaths) AS ?totalPedestrian)
    WHERE {
      ?pedestrian a btp:Pedestrain ;
                  btp:numberOfPedestrainInjuries ?pedestrianInjuries ;
                  btp:numberOfPedestrainDeath ?pedestrianDeaths ;
                  btp:recordedInDistrict ?district .
    }
    GROUP BY ?district
  }

  # Subquery for Scooter
  {
    SELECT ?district (SUM(?scooterInjuries + ?scooterDeaths) AS ?totalScooter)
    WHERE {
      ?scooter a btp:Scooter ;
               btp:numberOfScooterInjuries ?scooterInjuries ;
               btp:numberOfScooterDeath ?scooterDeaths ;
               btp:recordedInDistrict ?district .
    }
    GROUP BY ?district
  }

  # Query for accident count
  {
    SELECT ?district (SUM(?numberOfAccidents) AS ?numberOfAccidents)
    WHERE {
      ?roadaccident a btp:RoadAccident ;
                    btp:recordedInDistrict ?district ;
                    btp:numberOfAccidents ?numberOfAccidents .
    }
    GROUP BY ?district
  }

  # Calculate the max casualties among the categories and assign category label
  BIND(
    IF(?totalCyclist > ?totalDriver && ?totalCyclist > ?totalMotorcyclist && ?totalCyclist > ?totalPedestrian && ?totalCyclist > ?totalScooter, 
       "Cyclist", 
       IF(?totalDriver > ?totalMotorcyclist && ?totalDriver > ?totalPedestrian && ?totalDriver > ?totalScooter, 
          "Driver", 
          IF(?totalMotorcyclist > ?totalPedestrian && ?totalMotorcyclist > ?totalScooter, 
             "Motorcyclist", 
             IF(?totalPedestrian > ?totalScooter, 
                "Pedestrian", 
                "Scooter"
             )
          )
       )
    ) AS ?category
  )

  # Calculate max casualties value
  BIND(
    IF(?totalCyclist > ?totalDriver && ?totalCyclist > ?totalMotorcyclist && ?totalCyclist > ?totalPedestrian && ?totalCyclist > ?totalScooter, 
       ?totalCyclist, 
       IF(?totalDriver > ?totalMotorcyclist && ?totalDriver > ?totalPedestrian && ?totalDriver > ?totalScooter, 
          ?totalDriver, 
          IF(?totalMotorcyclist > ?totalPedestrian && ?totalMotorcyclist > ?totalScooter, 
             ?totalMotorcyclist, 
             IF(?totalPedestrian > ?totalScooter, 
                ?totalPedestrian, 
                ?totalScooter
             )
          )
       )
    ) AS ?maxCasualties
  )
}
GROUP BY ?district
